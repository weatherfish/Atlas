group = 'com.taobao.android.atlasdemo'
//版本号
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "");

apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'

repositories {
    mavenLocal()
    jcenter()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId "com.taobao.demo"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName version
        vectorDrawables.useSupportLibrary = true
    }


    dataBinding {
        enabled = true
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            ndk {
                abiFilters "x86","armeabi"
            }
        }
    }

    //    productFlavors {
    //        dev {
    //            minSdkVersion 14
    //        }
    //
    //        beta {
    //            minSdkVersion 21
    //        }
    //    }
}

atlas {

    atlasEnabled true   //开启打包阶段的基于容器扩展的task
    tBuildConfig {
        autoStartBundles = ['com.taobao.firstbundle'] //自启动的bundle列表， 值是 packageName
        outOfApkBundles = ['remotebundle']  //远程Bundle
        preLaunch = 'com.taobao.demo.DemoPreLaunch' //提前执行的方法，必须实现AtlasPreLauncher接口
        classInject false
    }

    manifestOptions {
        addAtlasProxyComponents true
    }


    patchConfigs {
        debug {
            //打动态部署 patch 包
            createTPatch true

            //打andfix patch 包
            //createAPatch true
            //andfix 打包过滤 class 列表文件
            //filterFile xx
            //andfix 打包过滤 class 列表文件
            //filterClasses []
        }
    }


    buildTypes {
        debug {
            if (apVersion) {
                //基线的依赖坐标
                baseApDependency "com.taobao.android.atlasdemo:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile('com.taobao.android:atlas_core:5.0.7.11@aar') {

        transitive = true
    }


    compile project(':middlewarelibrary')
    compile project(':splashscreen')
    compile project(':activitygroupcompat')

    bundleCompile project(':firstbundle')
    bundleCompile project(':secondbundle')
    bundleCompile project(':remotebundle')
    bundleCompile project(':publicbundle')
    //    bundleCompile project(':databindbundle')

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-vector-drawable:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.taobao.android:atlasupdate:1.1.4.10@aar'

    compile 'com.alibaba:fastjson:1.1.45.android@jar'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:recyclerview-v7:25.3.0'
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("DebugAndroidTest")) {
        task.setEnabled(false);
    }
    if (task.name.contains("assemble")) {
        def files = null;
        def file = new File(task.project.getBuildDir(), "outputs");
        if (file.exists() && new File(file, "tpatch-debug").exists()) {
            files = new File(file, "tpatch-debug").listFiles();
        }
        if (files != null) {
            for (File file1 : files) {
                if (file1.getName().endsWith(".json") || file1.getName().endsWith(".tpatch")) {

                    if (!new File(task.project.getRootDir(), "hisTpatch").exists()) {
                        new File(task.project.getRootDir(), "hisTpatch").mkdirs();
                    }
                    org.apache.commons.io.FileUtils.copyFileToDirectory(file1, new File(task.project.getRootDir(), "hisTpatch"));
                }
            }
        }
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
            artifactId "AP-debug"
        }
    }
}
